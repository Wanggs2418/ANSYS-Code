! http://www.1cae.com/a/ansys/55/ansys-9815.htm
! 子模型应用实例
! 内容：矩形平板中心开孔,承受横向拉力时的应力集中情况
! 边界：由于对称,选取模型的四分之一进行分析
! ------------------------------------
! 建立整体模型
finish
/clear,nostart
/FILNAM,coarse        ! 整体模型
/PREP7
smrt,off
/TITLE, STRESS CONCENTRATION AT A HOLE IN A PLATE
/NOPR
ANTYPE,STATIC         ! 静力分析
ET,1,PLANE2
MP,EX,1,30E6
MP,NUXY,1,0.3
! 圆孔板的1/4几何模型
K,1,6                 ! 关键点
K,2,6,6
K,3,,6
K,4,,.5
K,5,.5
K,6
L,1,2                 
L,2,3
L,3,4
! 划分网格
LESIZE,3,,,4,.25      ! 定义线3的分段数
LARC,4,5,6,0.5
LESIZE,4,,,6          ! 定义线4的分段数 
L,5,1
LESIZE,5,,,4,4        ! 定义线5的分段数
AL,1,2,3,4,5         
ESIZE,,4             ! 每条线分为4段
AMESH,ALL        
! 施加约束
LSEL,S,LINE,,3,5,2
DL,ALL,,SYMM
LSEL,S,LINE,,1
NSLL,,1
SF,ALL,PRES,-1000.    ! 施加拉力
LSEL,ALL
NSEL,ALL
CSYS,1
FINISH
! 求解
/SOLU    
SOLVE
FINISH
SAVE                  ! 存储文件VM142.DB
! 后处理导出边界条件
/POST1
SET,1,1
NSORT,S,X,,,3   ! 默认按照 x方向的应力对节点进行降序排列，保留前3位      
PRNSOL,S,COMP   ! 查看前3位节点的应力，其中Sx最大值对应的节点号为18
PLNSOL,S,X
*GET,CRSESTR,NODE,18,S,X    ! 获取18号节点的应力(Sx最大值)
*STATUS         ! 查看参数的值
*DIM,LABEL,CHAR,1,2 ! 字符型数组:1×2
*DIM,VALUE,,1,3     ! 数值型数组：1×3
! 数组赋值
LABEL(1,1) = 'MX STR '
LABEL(1,2) = 'CRS MODEL'
*VFILL,VALUE(1,1),DATA,3018     ! 3018为理论解
*VFILL,VALUE(1,2),DATA,CRSESTR
*VFILL,VALUE(1,3),DATA,ABS(CRSESTR/3018)
SAVE,TABLE_1
FINISH

! ------------------------------------
! 建立子模型
/CLEAR, NOSTART ! CLEAR THE DATABASE
/FILNAM,SUBMODEL      ! 定义子模型文件名
/PREP7
smrt,off
/NOPR
/TITLE, STRESS CONCENTRATION AT A HOLE IN A PLATE
ANTYPE,STATIC
ET,1,PLANE42
MP,EX,1,30E6
MP,NUXY,1,0.3
! 子模型几何建模
CSYS,1
K,10,.5,45            
K,11,.5,90
K,12,1.5,45
K,13,1.5,90
A,10,12,13,11
! 网格划分
ESIZE,,8             
MSHK,1                
MSHA,0,2D            
AMESH,1     
! 选择切割的边界节点
EPLOT
LSEL,S,LINE,,1,2
NSLL,,1               ! 选择切割边界的结点
NWRITE                ! 将结点写入文件SUBMODEL.NODE
LSEL,ALL
NSEL,ALL
FINISH
SAVE                  ! 存储子模型数据库文件SUBMODEL.DB

! 施加子模型边界条件
/POST1
RESUME,coarse,db       
FILE,coarse,rst        
CBDOF,,,,,,,0,,0      !激活边界条件插值(子模型分析).cbdo文件
FINISH
/PREP7
smrt,off
RESUME                !从文件SUBMODEL.DB中读入子模型
/INPUT,,cbdo,,:cb1    ! 从文件SUBMODEL.CBDO中读入插值边界条件,从cb1标签开始读取数据
LSEL,S,LINE,,3        ! 施加其余的边界条件
DL,ALL,,SYMM
FINISH
! 子模型求解
/SOLU    
SOLVE
FINISH
! 提取子模型和理论解的信息
/POST1
SET,1,1
NSORT,S,X,,,3
PRNSOL,S,COMP  
PLNSOL,S,X
*GET,SUBSTR,NODE,18,S,X
*DIM,LABEL,CHAR,1,2
*DIM,VALUE,,1,3
LABEL(1,1) = 'MAX STRS'
LABEL(1,2) = ' SUBMOD'
*VFILL,VALUE(1,1),DATA,3018
*VFILL,VALUE(1,2),DATA,SUBSTR
*VFILL,VALUE(1,3),DATA,ABS(SUBSTR/3018 )
SAVE,TABLE_2
FINISH