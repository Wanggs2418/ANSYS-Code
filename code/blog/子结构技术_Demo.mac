!生成子结构:
FINISH$/CLEAR!!!（清除）
/FILNAME,CREAT!生成子结构的文件名
/PREP7$B=1000$H=2000$R=200
B1=600$H1=1200$HB=1200

!创建空腔部分(1/4)
BLC4,,,B1/2,H1/2
CYL4,B1/2-R,H1/2-R,R,,,90
AOVLAP,ALL
ASEL,S,LOC,Y,H1/2-R,H1/2
ASEL,R,LOC,X,B1/2-R/2,B1/2
ADELE,ALL,,,1$ASEL,ALL
AADD,ALL
NUMMMP,ALL

!创建面(1/4)
BLC4,,,B/2,H/2$ASBA,2,1
WPOFF,B1/2-R$WPROTA,,,90$ASBW,ALL
WPOFF,,H1/2-R,R
WPROTA,,90$ASBW,ALL
WPOFF,,,-R-R/2$ASBW,ALL
KWPAVE,KP(B/2,H1/2+R/2,0)
WPROTA,,-45$ASBW,ALL$WPCSYS,-1
K,500,0,0,0$K,501,0,0,HB
L,500,501
LINE1=_RETURN   ! 是否创建成功
VDRAG,ALL,,,,,,LINE1
LDELE,LINE1

!划分网格(单元尺寸50)
ET,1,185$MP,EX,1,2.1E5
MP,PRXY,1,0.3$MP,DENS,1,1.0
ESIZE,50$MSHKEY,1$MSHAPE,0
VMESH,ALL

!生成整个子结构的模型
VSYMM,X,ALL
VSYMM,Y,ALL
NUMMRG,ALL
NUMCMP,ALL
FINISH

/SOLU
ANTYPE,SUBST!子结构分析
SEOPT,MYZJG!子结构分析选项，定义子结构名字MYZJG
CMSOPT,FIX,1
!两个侧面节点(子结构与子结构的连接点)
NSEL,S,LOC,X,-B/2
NSEL,A,LOC,X,B/2
M,ALL,ALL!定义主自由度
!施加荷载(也可不在这里施加荷载，
!但需将施加荷载的点定义为主自由度)
ASEL,S,LOC,Y,H/2
SFA,ALL,1,PRES,1
ALLSEL,ALL
SAVE!保存,CREAT.DB,扩展时用
SOLVE
FINISH
!!===========================

!使用子结构（超单元)
FINISH
/CLEAR!!!!
/FILNAME,USEFJG!完整结构文件名
/PREP7$B=1000$H=2000$R=200.0
B1=600$H1=1200$L=10000$HB=1200
ET,1,SOLID185$ET,2,MATRIX50
MP,EX,1,2.1E5$MP,PRXY,1,0.3

!创建非子结构部分的模型和网格
!该部分与子结构相连的节点必须重合，以便耦合
BLC4,,-H/2,B/2,H,HB
BLC4,L-B/2,-H/2,B/2,H,HB
WPROTA,,90$VSBW,ALL
WPOFF,,,H1/2-R$VSBW,ALL
WPOFF,,,1.5*R$VSBW,ALL
WPCSYS,-1$WPROTA,,-90
WPOFF,,,H1/2-R$VSBW,ALL!
WPOFF,,,1.5*R$VSBW,ALL!
WPCSYS,-1$ESIZE,50$MSHKEY,1
TYPE,1$MAT,1$VMESH,ALL

!生成多个超单元（子结构）
TYPE,2
*DO,I,1,9
*GET,MAXN,NODE,,NUM,MAX
!从既有子结构文件创建一个超单元
SETRAN,MYZJG,,MAXN,MYZJG%I%,,,B*I
SE,MYZJG%I%!定义这个超单元
*ENDDO
!将超单元的主自由度耦合
ESEL,S,TYPE,,2$NSLE,S,ALL
CPINTF,ALL!图2(F)
!将非超单元与超单元主自由度连接(耦合)
ALLSEL$NSEL,S,LOC,X,B/2
NSEL,A,LOC,X,L-B/2
CPINTF,ALL!图2G)
ALLSEL,ALL!施加约束
ASEL,S,LOC,X,0$DA,ALL,ALL
!施加荷载(在非超单元上施加荷载)
ASEL,S,LOC,Y,H/2$SFA,ALL,1,PRES,1
!施加超单元荷载(荷载系数)
ESEL,S,TYPE,,2
SFE,ALL,1,SELV,,1
ALLSEL,ALL!
SAVE !保存整个模型,即USEFJG.DB
/SOLU$SOLVE$FINISH

/POST1$PLNSOL,S,X!图2H)
!将超单元之外的结果写入文件FJGFC.RST中
!在总查看整个模型结果时用
ESEL,U,TYPE,,2
RSPLIT,ALL,ESEL,FJGFC
FINISH!

!!!!===========================
!扩展求解子结构中的结果
FINISH$/CLEAR!!!!!
/FILNAME,CREAT!切换文件名为生成时
RESUME!恢复数据库
!将运行后的结果文件赋予MYZJG1.RST
/ASSIGN,RST,MYZJG1,RST
/SOLU$EXPASS,ON!扩展子结构1
!查看整个模型结果时用，
!OFFNM源于非超单元节点总数或单元数
!RSTOFF和CMSxxx用于总体模型中时的设置，否则不用使用。
OFFNM=250000
SEEXP,MYZJG1,USEFJG,,ON
NUMEXP,ALL
RSTOFF,NODE,OFFNM
RSTOFF,ELEM,OFFNM
SOLVE$FINISH!!!!!

!可以查看子结构1的结果
! /POST1$PLNSOL,U,SUM
! PLNSOL,S,EQV$PLNSOL,S,X
!扩展其余子结构，用循环(示例而已)
*DO,I,2,9
FINISH!!
/ASSIGN,RST,MYZJG%I%,RST
/SOLU$EXPASS,ON
SEEXP,MYZJG%I%,USEFJG,,ON
NUMEXP,ALL!
RSTOFF,NODE,OFFNM I*30000
RSTOFF,ELEM,OFFNM I*30000
SOLVE!
/POST1$PLNSOL,S,X
/IMAGE,SAVE,T%I%SX,PNG!出图
*ENDDO!
!============================

!查看整个模型的结果
FINISH$/CLEAR!!!!!!!
!切换文件名为使用的文件名
/FILNAME,USEFJG
RESUME!恢复USEFJG.DB
/POST1!后处理
CMSFILE,ADD,FJGFC,RST,OFF
ZJ=5
CMSFILE,ADD,MYZJG%ZJ%,RST,OFF
SET,LAST$PLNSOL,S,X
!下面为全部加进子结构结果
!*DO,I,1,9
!CMSFILE,ADD,MYZJG%I%,RST,OFF
!*ENDDO
!SET,LAST$PLNSOL,U,SUM
