! EX4.46-风压时程作用 P156
! 风压时程数据在文件 wp600.txt 中，时程总长 600s,9646个数据点
finish
/clear
/config, nres, 30000
/prep7
a = 2.0
b = 1.2
h = 1.3
c = 0.4
! 单元类型
et, 1, shell181
et, 2, beam188
r, 1, 0.003
mp, ex, 1, 2.1e11
mp, prxy, 1, 0.3
mp, dens, 1, 7850

! 创建模型并施加约束
blc4,, h, a, b
wprota,,, 90
wpoffs,,, c
asbw, all
wpoffs,,, (a-2*c)
asbw, all
k, 20, c
k, 21, a-c
l, 20, kp(c,h,0)
l, 21, kp(a-c,h,0)
aatt, 1, 1, 1
esize, 0.1
mshkey, 1
amesh, all
k, 100, -c
k, 200,, 2*a
! 属性
lsel, s, loc, y, 0, h-0.1
latt, 1,, 2,,, 100, 2
esize, 0.2
lmesh, all
lsel, s, loc, y, h
lsel, a, loc, y, h+b
latt, 1,, 2,,, 200, 2
lmesh, all
lsel, s, loc, x, 0
lsel, a, loc, x, a
latt, 1,, 2,,, 100, 2
lmesh, all
allsel, all
dk, 20, all
dk, 21, all

! 定义施加风压的梁单元元件
lsel, s, tan1, y
lsel, r, loc, x, 0, a-c
esll, s
cm, beam1p, elem
lsel, s, tan1, y
lsel, r, loc, x, a-c, a
esll, s
cm, beam2p, elem
lsel, s, loc, x, 0
esll, s
cm, beam3p, elem
lsel, s, loc, x, a
esll, s
cm, beam4p, elem
allsel, all

! 读入风压时程数据，共9646个数据点
nn = 9646
*dim, wp,, nn, 2
*vread, wp, wp600, txt,, jik, 2, nn
(2f10.2)

! 定义瞬态分析选项,循环加载风压时程并求解
/solu
antype, trans
outres, all, all
nlgeom, on
autots, on
kbc, 0  ! ramped load
nsubst, 2
*do, i, 1, nn
time, wp(i,1)
sfa, all, a, pres, -wp(i,2)
sfbeam, beam1p, 2, pres, -wp(i,2)*0.025
sfbeam, beam2p, 2, pres, wp(i,2)*0.025
sfbeam, beam3p, 2, pres, wp(i,2)*0.025
sfbeam, beam4p, 2, pres, -wp(i,2)*0.025
solve
*enddo

! 后处理
! 将面板中心位移定为变量2，绘制时程曲线
/post1
set,,,,, 106
plnsol, u, z
plnsol, s, eqv