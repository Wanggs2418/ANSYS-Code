! EX4.06-简谐运动下单自由度瞬态动力分析 P105
! 直接加载分析
finish
/clear
/config, nres, 5000
! 设置表数组
*dim, myf, table, 6, 6, 1
myf(0,0,1) = 0, -999, 0, 0, 0, 0, 0
myf(0,1,1) = 1, -1, 0, 5, 0, 0, 1
myf(0,2,1) = 0, -2, 0, 1, -1, 3, 1
myf(0,3,1) = 0, -1, 10, 1, -2, 0, 0
myf(0,4,1) = 0, -2, 0, 40, 0, 0, -1
myf(0,5,1) = 0, -3, 0, 1, -2, 3, -1
myf(0,6,1) = 0, 99, 0, 1, -3, 0, 0

/prep7
k = 1000
m = 10
c = 15
p0 = 40
uomg = 5
et, 1, combin14,,, 2
et, 2, mass21,,, 4
r, 1, k, c
r, 2, m
n, 1
n, 2, 1
! 弹簧单元
type, 1
real, 1
e, 1, 2
! 质量单元
type, 2
real, 2
e, 2
d, 1, all
d, all, uy

! 瞬态动力分析
/solu
antype, trans
trnopt, full
timint, on
autots, on
outres, all, all
f, 2, fx, %myf%
! 第1荷载步时长任意，第1子步时间很小
time, 0.005
deltim, 1e-6
solve
time, 7
deltim, 0.005,, 0.01
solve

! 后处理
! 位移时程和加速度时程
/post26
nsol, 2, 2, u, x, disp
plvar, 2
nsol, 3, 2, a, x, acel
plvar, 3
force, total
rforce, 4, 1, f, x
plvar, 4