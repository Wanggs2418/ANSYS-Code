! Π型悬臂梁EX7.10 P282
! 壳体转折处的处理
finish
/clear
/prep7
l = 2000
! 距端部200mm的截面
a = 200
b = 100
t1 = 12
t2 = 10
blc4,,, l, a
wprota,, -90
wpoffs,,, t2/2
blc4,,, l, b-t1/2
agen, 2, 2,,, 0, a-t2
aglue, all
! 在所求截面a=200mm位置切分图形
wpcsys, -1
wpoffs, a
wprota,,, 90
asbw, all
! 在中性轴切分模型
wpoffs, 27.15-t1/2
wprota,,, 90
asbw, all
wpcsys, -1
! 定义单元类型和属性
et, 1, shell181
keyopt, 1, 3, 2
mp, ex, 1, 2e5
mp, prxy, 1, 0.3
! 四个节点对应的厚度
r, 1, t1
r, 2, t2
! 属性赋予
asel, s, loc, z, 0
aatt, 1, 1, 1
! 施加荷载
sfa, all, 2, pres, 0.02
asel, inve  !反选
aatt, 1, 2, 1
! 划分网格
asel, all
mshkey, 1
esize, 20
amesh, all
! 施加约束
lsel, s, loc, x, 0
dl, all,, all
allsel, all
/solu
solve

! 后处理
/post1
/view, 1, 1, 1, 1
/ang, 1, -120, zs
plnsol, u, z
asel, s, loc, x, a, l
esla
plnsol, s, x
! 绘制中面的应力云图
! 默认为top
shell, mid
plnsol, s, x






