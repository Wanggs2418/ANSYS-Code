! 结构分析中的滑动单元, P503
finish
/clear
/prep7
l1 = 1000
l2 = 400
l3 = 100
l4 = 500
cita = 30*acos(-1)/180
et, 1, beam3
et, 2, mpc184   ! keyopt(1)=3滑块单元
keyopt, 2, 1, 3
mp, ex, 1, 2e5
mp, prxy, 1, 0.3
r, 1, 80, 500, 10
! 几何模型
k, 1
k, 2, l1
l, 1, 2
k, 3, 0, -(l1-l2)*tan(cita)
k, 4, l2, -(l1-l2)*tan(cita)
l, 3, 4
k, 5, l1+l3, l3*tan(cita)
k, 6, l1+l3+l4, l3*tan(cita)
l, 5, 6
latt, 1, 1, 1
esize, 20
lmesh, all
! 获得节点号并通过节点定义MPC184-3滑块单元
! 节点i在j,k之间运动
ni = node(kx(2), ky(2), kz(2))
nj = node(kx(4), ky(4), kz(4))
nk = node(kx(5), ky(5), kz(5))
type, 2
e, ni, nj, nk   ! 借助节点直接创建滑块单元
dk, 1, all
dk, 3, all
dk, 6, all
f, 2, fy, -2000
! 求解查看结果
/solu
nlgeom, on
outres, all, all
time, 1
nsubst, 20
solve

! 后处理
/post1
plnsol, u, sum
antime, 50, 0.2 ! 50帧，0.2秒


