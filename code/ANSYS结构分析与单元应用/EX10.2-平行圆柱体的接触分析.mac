! EX10.2 平行圆柱体的接触分析 P395
! 主要影响因素未网格样式和密度，接触刚度和侵入容差影响较小
! 调整网格样式和密度后，模型能量误差 < 5%（prerr）结果可信
finish
/clear
/prep7

r1 = 10
r2 = 5
d1 = r1/5
d2 = r2/5
et, 1, solid95
et, 2, conta174
et, 3, targe170
keyopt, 2, 5, 4 ! 自动ICONT
r, 1 ! 缺省值
r, 2
mp, ex, 1, 2.1e5
mp, prxy, 1, 0.3
/view, 1, 1, 1, 1

! 几何模型-下部
cyl4,,, r1, 90,,, 1
cyl4,, r1, d1, -90,,, 1
vovlap, 1, 2
! 删除无用的图素
vdele, 4,,, 1
numcmp, all
! 几何模型-上部
cyl4,, r1+r2, r2, -90,,, 1
cyl4,, r1, d2, 90,,, 1
vovlap, 3, 4
vdele, 6,,, 1
numcmp, all
vatt, 1, 1, 1
! 网格划分
lsel, s, length,, 1
lsel, u, tan1, z    ! 仅选择厚度方向线段
lesize, all,,, 4    ! 厚度方向划分4段
lsel, all
mshkey, 1
vsel, s,,, 1, 3, 2  ! 仅划分接触区的小圆弧体
esize, 0.1
vmesh, all
vsel, inve  ! 反选剩余体
esize, 1.0
vmesh, all

! 选择节点和接触单元
asel, s,,, 15
nsla, s, 1
type, 2
real, 2
esurf
asel, s,,, 5
nsla, s, 1
type, 3
esurf
allsel, all
! 位移约束
asel, s, loc, y, r1+r2
da, all, uy, -0.03
! 竖向及对称约束
asel, s, loc, y, 0
da, all, uy
asel, s, loc, x, 0
da, all, symm
! 厚度方向约束
asel, s, loc, z, 0
asel, a, loc, z, 1
da, all, uz
allsel, all

/solu
time, 1
nsubst, 10
solve

/post1
esel, s, type,, 2
presol, cont    ! 结果弹窗
plnsol, cont, pres
plnsol, cont, pene  ! 侵入
plnsol, cont, stat  ! 状态
plnsol, cont, gap   ! 间隙
! 定义单元表
esel, s, type,, 2
etable, fxi, nmisc, 43
etable, fyi, nmisc, 44
etable, fzi, nmisc, 45
ssum
*get, sfor3x, ssum, 0, item, fxi
*get, sfor3y, ssum, 0, item, fyi
*get, sfor3z, ssum, 0, item, fzi
allsel, all
! 输出所有单元的能量误差模百分比
! /graphics, defa
/graphics, full
prerr
! 结果拓展
/expand, 2, rect, half, 1e-8
