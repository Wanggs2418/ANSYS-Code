! EX10.28远距离加载 P446
! cont174-targe170
! 约束面的及其用法
finish
/clear
/prep7
r0 = 100
l0 = 2000
b = 500
p = 10000
et, 1, solid95
et, 2, targe170
et, 3, conta174
! 导向节点约束条件由用户施加,keyopt(2)=1
keyopt, 2, 2, 1
keyopt, 3, 2, 2 ! MPC 算法
keyopt, 3, 4, 1 ! 荷载分配约束面,接触检测点为接触面法向节点位置
! keyopt, 3, 4, 2 ! 刚性约束面
keyopt, 3, 12, 5    ! 永久绑定
mp, ex, 1, 2e5
mp, prxy, 1, 0.3
! mp, prxy, 1, 0.0

! 创建几何模型并划分网格
cyl4,,, r0,,,, l0
wprota,, 90
vsbw, all
wprota,,, 90
vsbw, all
vatt, 1, 1, 1
esize, r0/2
mshkey, 1
vmesh, all

! 定义接触
asel, s, loc, z, l0
nsla, s, 1
type, 3 ! 接触单元
real, 2
esurf
nmax = ndinqr(0, 14)+1
n, nmax, b, 0, l0
type, 2
tshap, pilo
e, nmax

! 施加荷载和约束条件，求解并查看结果
f, nmax, fy, -p
nsel, s, loc, z, 0
d, all, all
allsel, all
/solu
solve
celist,,,, inte

! 后处理
/post1
plnsol, u, sum
plnsol, s, z
plnsol, s, xz