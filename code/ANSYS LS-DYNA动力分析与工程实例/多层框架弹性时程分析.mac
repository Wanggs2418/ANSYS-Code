! ansys18.0-ANSYS/LS-DYNA模块
! 多层框架结构的弹性时程分析 P237
finish
/clear
/filname, building
/prep7

! ----------------------
! LS-DYNA单元类型
! ----------------------
et, 1, beam161  ! 梁柱
et, 2, shell163  ! 板
r, 1    ! 梁单元实参
rmodif, 1, 1, 5/6, 0.5, 0.5, 0.25, 0.25
rmodif, 1, 6, 0, 0
r, 2    ! 柱单元实参
rmodif, 2, 1, 5/6, 0.4, 0.4, 0.4, 0.4
rmodif, 2, 6, 0, 0
r, 3    ! 板单元实参
rmodif, 3, 1, 5/6,, 0.1, 0.1, 0.1, 0.1

! ----------------------
! 材料属性
! ----------------------
mp, dens, 1, 2500   ! 钢筋混凝土密度
mp, ex, 1, 3.0e10   ! 折算弹模
mp, nuxy, 1, 0.2    ! 线弹性材料

! ----------------------
! 直接创建节点
! ----------------------
! 基础表面节点
n, 1, 0, 0, 0
n, 2, 5, 0, 0
n, 3, 10, 0, 0
n, 4, 0, 0, 5
n, 5, 5, 0, 5
n, 6, 10, 0, 5
! 复制并删除多余节点
ngen, 7, 6, 1, 6, 1, 0, 3, 0
nsel, s, loc, x, 9.5, 10.5
nsel, r, loc, y, 9.5, 18.5
ndele, all
allsel, all

! 梁柱单元的定位点
n, 100, 0, 50, 0
n, 101, 5, 50, 5
n, 102, 10, 50, 5

! 建立板单元
type, 2
mat, 1
real, 3
! 左半跨各层单元
*do, i, 7, 37, 6
    e, i, i+3, i+4, i+1
*enddo
! 右跨各层单元
*do, i, 8, 20, 6
    e, i, i+3, i+4, i+1
*enddo

! 梁柱单元
type, 1
mat, 1
real, 1
! 注意梁柱单元的定位点
! 左半跨各层梁单元
*do, i, 10, 40, 6
    e, i, i+1, 101
    e, i-3, i-2, 100
    e, i, i-3, 100
    e, i+1, i-2, 101
*enddo
! 右半跨各层梁单元
*do, i, 11, 23, 6
    i =17
    e, i, i+1, 101
    e, i-3, i-2, 100
    e, i+1, i-2, 102
*enddo
! 框架柱,左边柱和中间柱
type, 1
mat, 1
real, 2
*do, i, 1, 31, 6
    e, i, i+6, 2
    e, i+1, i+7, 100
    e, i+4, i+10, 2
    e, i+3, i+9, 1
*enddo
! 右边柱
*do, i, 3, 15, 6
    e, i, i+6, 2
    e, i+3, i+9, 3
*enddo

! 基底节点水平方向自由度耦合
cp, 1, ux, 1, 2, 3, 4, 5, 6
cp, 3, uz, 1, 2, 3, 4, 5, 6

! ----------------------
! 边界约束
! ----------------------
nsel, s, loc, y, -0.1, 0.1  ! 基础表面各点
d, all,, 0,,,, uy, rotx, roty, rotz

! 基底施加水平方向地震波加速度
! 荷载-时间变量数组
*dim, time, array, 301, 1, 1
*dim, acc, array, 301, 1, 1
*do, i, 1, 301, 1
    time(i,1,1) = (i-1)*0.01
    acc(i,1,1) = sin(100*((i-1)*0.01))+sin(110*((i-1)*0.01))+sin(120*((i-1)*0.01))
    acc(i,1,1) = acc(i,1,1)*500*sin(10*((i-1)*0.01))*(1-cos(2*((i-1)*0.01)))
*enddo

! 建立由所有基底节点组成的结构受载节点组元
cm, base, node

! 向节点组元施加加速度-时间历程
edload, add, ax,, base, time, acc
allsel, all

! -----------------------
! 计算设置及输出选项
! -----------------------
eddamp, all, 0, 1   ! 定义阻尼
/solu
time, 5
edopt, add, blank, lsdyna
edrst, 300  ! 指定d3plot文件输出的步数
edhtime, 1  ! 指定d3thdt输出的步数
eddump, 1   ! 指定重启动记录的次数
edwrite, lsdyna, building, k